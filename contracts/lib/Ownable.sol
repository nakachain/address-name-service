pragma solidity ^0.5.7;

/**
 * @title Ownable
 * @dev The Ownable contract has an owner address and provides basic 
 * authorization control functions. This simplifies the implementation of 
 * "user permissions". This is a modified version of the standard Ownable
 * implementation where the initial owner can be passed in the constructor.
 */
contract Ownable {
    address private _owner;

    event OwnershipTransferred(
        address indexed previousOwner,
        address indexed newOwner
    );

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(isOwner(), "Owner is only allowed to call this method.");
        _;
    }

    /**
     * @dev Throws if address is not valid.
     * @param addr Address to validate.
     */
    modifier validAddress(address addr) {
        require(addr != address(0), "Requires valid address.");
        _;
    }

    /**
     * @dev The Ownable constructor sets the original `owner` of the contract.
     * @param owner Initial owner of the contract.
     */
    constructor(address owner) internal {
        _owner = owner;
        emit OwnershipTransferred(address(0), _owner);
    }

    /**
     * @dev Allows the current owner to transfer control of the contract to a
     * newOwner.
     * @param newOwner The address to transfer ownership to.
     */
    function transferOwnership(address newOwner) public onlyOwner {
        require(newOwner != address(0), "Requires valid address for new owner.");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }

    /**
     * @dev Allows the current owner to relinquish control of the contract.
     * @notice Renouncing to ownership will leave the contract without an owner.
     * It will not be possible to call the functions with the `onlyOwner`
     * modifier anymore.
     */
    function renounceOwnership() public onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @return the address of the owner.
     */
    function owner() public view returns (address) {
        return _owner;
    }

    /**
     * @return true if `msg.sender` is the owner of the contract.
     */
    function isOwner() public view returns (bool) {
        return msg.sender == _owner;
    }
}
